# Snakefile

configfile: "config.yaml"

rule all:
    input:
        expand(config["filtered_chr_dir"] + "/" + "pruned_grm_{ancestry}.rel.id", ancestry=config["Ancestry"])

# Step 1: Filter SNP List per Ancestry

rule filter_snplist_per_ancestry:
    input:
        bfile=config["geno_dir"] + "/" + "ukb22418_c{chr}_b0_v2_s488243" + config["EXT"],
        sample_ids=config["sample_ids_dir"] + "/" + "{ancestry}_ids.txt"
    output:
        SNPLIST=config["snp_list_dir"] + "/" + "{ancestry}_filtered_all_chr{chr}.snplist"
    params:
        plink_executable = config["plink_executable"],
        THREADS=4,
        MAF=config["MAF"],
        GENO=config["GENO"],
        HWE=config["HWE"],
        MAC=config["MAC"],
        IPREFIX=config["geno_dir"]+ "/" + "ukb22418_c{chr}_b0_v2_s488243",
        OPREFIX="{ancestry}_filtered_all_chr{chr}",
        SNPDIR=config["snp_list_dir"]
    resources:
        cpus=4, mem_mb=20000, time_min=1440
    shell:
        """
        cd {params.SNPDIR}
        
        {params.plink_executable} \
        --bfile {params.IPREFIX} \
        --keep {input.sample_ids} \
        --maf {params.MAF} \
        --mac {params.MAC} \
        --geno {params.GENO} \
        --hwe {params.HWE} \
        --keep-allele-order \
        --threads {params.THREADS} \
        --write-snplist \
        --out {params.OPREFIX}
        """

# Step 1.5 Create Common SNP list across Ancestries

rule common_snps:
    input:
        expand(config["snp_list_dir"] + "/" + "{ancestry}_filtered_all_chr{chr}.snplist",ancestry=config["Ancestry"], chr=config["CHR"])
    output:
        config["common_snps_dir"] + "/" + "common_snps_all.txt"
    params:
        INPUTDIR=config["snp_list_dir"],
        SCRIPT=config["SCRIPT"] + "/" + "common_snps.R"
    resources: cpus=1, mem_mb=20000, time_min=1440
    shell:
        """
        ml R/4.1.2
        Rscript {params.SCRIPT} -d {params.INPUTDIR} -o {output}
        """

# Step 2: Filtered CHR for Common snps

rule filtered_chr_common_snps:
    input:
        bfile=config["geno_dir"] + "/" + "ukb22418_c{chr}_b0_v2_s488243" + config["EXT"],
        common_snps=config["common_snps_dir"] + "/" + "common_snps_all.txt"
    output:
        CHR_filtered=config["filtered_chr_dir"] + "/" + "filtered_chr{chr}.bed"
    params:
        plink_executable = config["plink_executable"],
        GENO=config["GENO"],
        THREADS=4,
        IPREFIX=config["geno_dir"]+ "/" + "ukb22418_c{chr}_b0_v2_s488243",
        OPREFIX="filtered_chr{chr}",
        CHRDIR=config["filtered_chr_dir"]
    resources:
        cpus=4, mem_mb=20000, time_min=1440
    shell:
        """
        cd {params.CHRDIR}
        
        {params.plink_executable} \
        --bfile {params.IPREFIX} \
        --extract {input.common_snps} \
        --make-bed \
        --keep-allele-order \
        --threads {params.THREADS} \
        --out {params.OPREFIX}
        """

# Step 2.5: create merge_list

rule create_merge_list:
    input:
        expand(config["filtered_chr_dir"] + "/" + "filtered_chr{chr}.bed", chr=config["CHR"]),
    output:
        merge_list=config["filtered_chr_dir"] + "/" + "merge_list.txt"
    resources:
        cpus=1, mem_mb=10000, time_min=1440
    shell:
        """
        ls {input} | xargs -n 1 basename | sed 's/.bed$//' > {output}
        """

# Step 2.5: Merged CHR for common snps

rule merge_chr_common_snps:
    input:
        merge_list=config["filtered_chr_dir"] + "/" + "merge_list.txt"
    output:
        merged_geno=config["filtered_chr_dir"] + "/" + "merged_geno_common_snps.bed"
    params:
        plink_executable = config["plink_executable"],
        THREADS=4,
        OPREFIX="merged_geno_common_snps",
        CHRDIR=config["filtered_chr_dir"]
    resources:
        cpus=4, mem_mb=80000, time_min=1440
    shell:
        """
        cd {params.CHRDIR}
        
        {params.plink_executable} \
        --merge-list {input.merge_list} \
        --make-bed \
        --keep-allele-order \
        --threads {params.THREADS} \
        --out {params.OPREFIX}
        """

# Step 3: Pruned GRM for each ancestry

rule pruned_grm_ancestry:
    input:
        bfile=config["filtered_chr_dir"] + "/" + "merged_geno_common_snps.bed",
        sample_ids=config["sample_ids_dir"] + "/" + "{ancestry}_ids.txt"
    output:
        GRM_pruned=config["filtered_chr_dir"] + "/" + "pruned_grm_{ancestry}.rel.id"
    params:
        plink_executable = config["plink_executable"],
        THREADS=10,
        IPREFIX=config["filtered_chr_dir"] + "/" + "merged_geno_common_snps",
        OPREFIX="pruned_grm_{ancestry}",
        CHRDIR=config["filtered_chr_dir"]
    resources:
        cpus=10, mem_mb=900000, time_min=3000
    shell:
        """
        cd {params.CHRDIR}
        
        {params.plink_executable} \
        --bfile {params.IPREFIX} \
        --keep {input.sample_ids} \
        --keep-allele-order \
        --threads {params.THREADS} \
        --rel-cutoff 0.05 \
        --out {params.OPREFIX}
        """
